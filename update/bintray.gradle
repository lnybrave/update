apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


String defaultValue(key, defaultValue) {
    if (ext.has(key)) ext[key]
    else defaultValue
}

version = package_version
group = package_group
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                // Add your description here
                name package_artifact
                description = defaultValue 'package_description', ''
                url = defaultValue 'siteUrl', ''

                if (!ext.has('package_type')){
                    if (project.getPlugins().findPlugin("com.android.library")){
                        packaging 'aar'
                    }else{
                        packaging 'jar'
                    }
                }else{
                    packaging package_type
                }
                groupId package_group
                artifactId package_artifact

                // Set your license
                licenses {
                    license {
                        name = defaultValue 'licenseName', ''
                        url = defaultValue 'licenseUrl', ''
                    }
                }
                developers {
                    developer {
                        id = defaultValue 'developerId', ''
                        name = defaultValue 'developerName', ''
                        email = defaultValue 'developerEmail', ''
                    }
                }
                scm {
                    connection = defaultValue 'gitUrl', ''
                    developerConnection = defaultValue 'gitUrl', ''
                    url = defaultValue 'siteUrl', ''
                }
            }
        }
    }
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = package_repo
        name = package_artifact
        desc = package_description
        websiteUrl = defaultValue 'siteUrl', ''
        vcsUrl = defaultValue 'gitUrl', ''
        licenses = defaultValue 'allLicenses', ''
        publish = true
    }
}
